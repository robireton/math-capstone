{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Even Fibonacci numbers\n",
    "\n",
    ">Each new term in the Fibonacci sequence is generated by adding the previous two terms. By starting with 1 and 2, the first 10 terms will be:\n",
    ">\n",
    ">1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...\n",
    ">\n",
    ">By considering the terms in the Fibonacci sequence whose values do not exceed four million, find the sum of the even-valued terms.\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The terms of the Fibonacci sequence get large pretty fast, so there won’t be very many of them that are smaller than 4,000,000:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "1\n",
      "2\n",
      "3\n",
      "5\n",
      "8\n",
      "13\n",
      "21\n",
      "34\n",
      "55\n",
      "89\n",
      "144\n",
      "233\n",
      "377\n",
      "610\n",
      "987\n",
      "1597\n",
      "2584\n",
      "4181\n",
      "6765\n",
      "10946\n",
      "17711\n",
      "28657\n",
      "46368\n",
      "75025\n",
      "121393\n",
      "196418\n",
      "317811\n",
      "514229\n",
      "832040\n",
      "1346269\n",
      "2178309\n",
      "3524578\n"
     ]
    }
   ],
   "source": [
    "a, b = 1, 2\n",
    "println(a)\n",
    "while b < 4000000\n",
    "    println(b)\n",
    "    a, b = b, a + b\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "That’s 32 terms, and only every third term is even (odd + even = odd; even + odd = odd; odd + odd = even; …) so that’s fewer terms still. We can really just collect the terms and then add them up:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "4613732"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function evenfibs(max)\n",
    "    a, b = zero(BigInt), one(BigInt)\n",
    "    terms = []\n",
    "    while b < max\n",
    "        iseven(b) && push!(terms, b)\n",
    "        a, b = b, a + b\n",
    "    end\n",
    "    terms\n",
    "end\n",
    "\n",
    "sum(evenfibs(4000000))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Iterators\n",
    "\n",
    "If we were working with a longer set of terms, it might help to write a custom iterator instead of trying to generate them all at once."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct Fib\n",
    "  max::Int\n",
    "end\n",
    "\n",
    "function Base.iterate(F::Fib, state=(1, 2))\n",
    "  a, b = state\n",
    "  if a > F.max\n",
    "      return nothing\n",
    "  end\n",
    "  (a, (b, a + b))\n",
    "end\n",
    "\n",
    "Base.eltype(::Type{Fib}) = Int\n",
    "Base.IteratorSize(F::Fib) = Base.SizeUnknown()"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now we can filter the numbers generated by the iterator and add them up:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "4613732"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "sum(Iterators.filter(iseven, Fib(4000000)))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Solution using a Julia Channel\n",
    "\n",
    "With this model, we have a *channel* (`F`), which the `fibonacci` function writes values to and which the other code pulls values from."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "4613732"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function fibonacci(c::Channel)\n",
    "  a, b = zero(BigInt), one(BigInt)\n",
    "  put!(c, a)\n",
    "  put!(c, b)\n",
    "\n",
    "  while true\n",
    "    put!(c, a + b)\n",
    "    a, b = b, a + b\n",
    "  end\n",
    "end\n",
    "\n",
    "total = 0\n",
    "F = Channel(fibonacci)\n",
    "while (n = take!(F)) < 4000000\n",
    "  iseven(n) && (total += n)\n",
    "end\n",
    "total"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The terms in this sequence grow exponentially and so we only need to consider a few of them before they equal or exceed $4\\times 10^{6}$. Indeed, only something like 34 terms need to be examined. This means that a direct approach of calculating them and adding them up one at a time will work quickly.\n",
    "\n",
    "A slight improvement over calculating each term in succession is to note that only every third term (terms number $2,5,8,\\ldots$) are even. It isn't hard to work out how to advance through the sequence by $3$ terms at a time.\n",
    "\n",
    "If we set $$X_n=\\left(\\begin{array}{c} f_n\\\\f_{n-1}\\end{array}\\right)$$ then \n",
    "\n",
    "$$X_{n+1}=\\left(\\begin{array}{cc} 1 &1 \\\\ 1&0 \\end{array}\\right)\\cdot X_{n-1}$$ so that the vectors \n",
    "\n",
    "$$X_{2+3k}=\\left(\\left(\\begin{array}{cc} 1 &1 \\\\ 1&0 \\end{array}\\right)^3\\right)^k\\cdot X_{2}$$ are the ones whose first component is an even Fibonacci number.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.4.1",
   "language": "julia",
   "name": "julia-1.4"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.4.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
